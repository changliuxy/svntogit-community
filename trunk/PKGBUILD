# Contributor: pressh <pressh funnysymbol gmail dot com>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: shaun lastra <badicalia@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gcolor2
pkgver=0.4
pkgrel=9
pkgdesc="A simple GTK+2 color selector"
arch=('x86_64')
url="http://gcolor2.sf.net/"
license=('GPL')
depends=('gtk2')
makedepends=('perl-xml-parser')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz" \
        gcolor2-0.4-amd64.patch gcolor2.desktop)
sha256sums=('89bd6c6d27ba5a679ec60057de8497855072f520515b490e5986fc3509774f23'
            '93b3218de6d522017cb2d14cb94cfe523100234a45fe9b46cee2274612887195'
            'b66b9af39e1cd09d4c7dbd7f76d6910e10586df3226c826d01cd9bfcb40519b3')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	[ "$CARCH" == "x86_64" ] && (patch -p1 < ../gcolor2-0.4-amd64.patch)

	# fix fs#25904
	sed -i 's/\[:space:\]/[&]/g' configure
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# install desktop file
	install -Dm644 "${srcdir}/gcolor2.desktop" \
		"${pkgdir}/usr/share/applications/gcolor2.desktop"
}
